buildscript {
    group = 'com.github.InteraactionGroup'
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'application'
    id 'distribution'
    id 'jacoco'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'io.freefair.lombok' version '6.4.3'
    id 'com.github.hierynomus.license-report' version '0.16.1'
    id 'com.github.spotbugs' version '6.1.11'
    id 'net.researchgate.release' version '2.6.0'
    id "de.undercouch.download" version '4.0.4'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'info.picocli:picocli-spring-boot-starter:4.7.5'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.5'
}

ext.javafxVersion = "21.0.1"

ext {
    Download = de.undercouch.gradle.tasks.download.Download
    download = download
}

apply from: "${rootDir}/gradle/package.gradle"
apply from: "${rootDir}/gradle/jre.gradle"
apply from: "${rootDir}/gradle/drivers.gradle"
apply from: "${rootDir}/gradle/installer.gradle"

javafx {
    version = javafxVersion
    modules = ['javafx.controls', 'javafx.swing', 'javafx.media', 'javafx.web']
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'org.openjfx.javafxplugin'
apply plugin: 'io.freefair.lombok'
apply plugin: 'com.github.hierynomus.license-report'
apply plugin: 'com.github.spotbugs'
apply plugin: 'distribution'

sourceSets {
    main {
        resources {
            srcDirs "src/resources"
        }
    }
}

allprojects {
    group = 'com.github.interaactionGaze'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven {
            url = 'https://jitpack.io'
        }
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'org.openjfx.javafxplugin'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.github.hierynomus.license-report'
    apply plugin: 'com.github.spotbugs'
    apply plugin: "pmd"
    apply plugin: 'distribution'

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/java')
            }
            resources.srcDir file('src/integration-test/resources')
        }
    }

    apply from: "${rootDir}/gradle/integration.gradle"

    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntime.extendsFrom testRuntime
    }

    javafx {
        version = javafxVersion
        modules = ['javafx.controls', 'javafx.swing', 'javafx.media', 'javafx.web']
    }

    spotbugs {
        toolVersion = '4.9.3'
    }

    spotbugsMain.enabled = true
    spotbugsTest.enabled = false

    downloadLicenses {
        dependencyConfiguration = 'compileClasspath'
        includeProjectDependencies = true
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
        testImplementation "org.testfx:testfx-core:4.0.16-alpha"
        testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
        testImplementation "org.testfx:openjfx-monocle:jdk-11+26"
        testImplementation "org.jmockit:jmockit:1.49"
        testImplementation "org.mockito:mockito-core:3.12.+"
    }

    sourceCompatibility = '17'
    targetCompatibility = '17'

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
        if (project.hasProperty('excludeTests')) {
            exclude project.property('excludeTests')
        }
    }

    tasks.withType(Test) {
        maxHeapSize = "3g"
    }

    jacoco {
        toolVersion '0.8.3' // We need this version for JMockit to work.
    }

    tasks.jacocoTestReport.dependsOn('test')
}


dependencies {
    implementation 'org.projectlombok:lombok:1.18.20'
    implementation files("${rootDir}/lib/EyeTribeJavaFx.jar")
    implementation 'com.github.GazePlay:TobiiStreamEngineForJava:5.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.rxtx:rxtx:2.1.7'
    implementation 'uk.org.lidalia:sysout-over-slf4j:1.0.2'
    implementation 'javax.media:jmf:2.1.1e'
    implementation 'org.json:json:20090211'
    implementation 'ws.schild:jave-all-deps:2.5.0'
    implementation 'ws.schild:jave-core:2.5.0'

    implementation 'org.slf4j:slf4j-api:2.0.9'
    runtimeOnly 'org.slf4j:jcl-over-slf4j:2.0.9'
    runtimeOnly 'org.slf4j:log4j-over-slf4j:2.0.9'
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Vendor": "Univ. Grenoble Alpes - LIG - GETALP",
                "Main-Class": 'application.Main',
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                "JavaFX-Version": javafxVersion,
                "Built-By": System.properties['user.name']
        )
    }
}

distributions {
    noJRE {
        setDistributionBaseName "${project.name}-no-jre"
        contents {
            with distributions.main.contents
            from("build/reports")
            from("build/bin") {
                into 'bin'
            }
        }
    }

    windows {
        setDistributionBaseName "${project.name}-windows-x64"
        contents {
            with distributions.main.contents
            from("build/reports")
            from("build/bin/interAACtionGaze-windows.bat") {
                into 'bin'
            }
            from("build/resources/main/AutoHotkey_2.0.11") {
                into 'lib/AutoHotkey'
            }
            from("build/jre/windows") {
                into 'lib/jre'
            }
        }
    }

    linux {
        setDistributionBaseName "${project.name}-linux"
        contents {
            with distributions.main.contents
            from("build/reports")
            from("build/bin/interAACtionGaze-linux.sh") {
                into 'bin'
            }
            from("build/bin/interAACtionGaze-linux-calibration.sh") {
                into 'bin'
            }
            from("build/jre/linux-x64") {
                into 'lib/jre'
            }
            from("build/tobiiDrivers") {
                into 'tobiiDrivers'
            }
        }
    }

    macos {
        setDistributionBaseName "${project.name}-macos"
        contents {
            with distributions.main.contents
            from("build/reports")
            from("build/bin/interAACtionGaze-macos.sh") {
                into 'bin'
            }
            from("build/jre/macos") {
                into 'lib/jre'
            }
        }
    }
}



tasks.jar.dependsOn('createLicence')

task createLicence(dependsOn: ['processResources'], type: Copy, group: 'license') {
    from("${rootDir}/gradle/templates/licence.txt")
    expand([
            "date"   : new Date().format('yyyy-MM-dd')
    ])
    into("${processResources.destinationDir}/data/common")
}

task('downloadAndExtractLinuxDependecies').dependsOn('downloadAndExtractLinuxJREwithLibavPlugin', 'downloadAndExtractTobiiDriversForLinux')

macosDistTar.dependsOn(downloadAndExtractMacosJRE)
windowsDistZip.dependsOn(downloadAndExtractWindowsJRE)
linuxDistTar.dependsOn(downloadAndExtractLinuxDependecies)

tasks.named('resolveMainClassName') {
    dependsOn tasks.named('createLicence')
    dependsOn tasks.named('unzipDistribution')
}

tasks.named('distTar') {
    dependsOn tasks.named('unzipDistribution')
}

tasks.named('distZip') {
    dependsOn tasks.named('unzipDistribution')
}

tasks.named('spotbugsMain') {
    dependsOn tasks.named('unzipDistribution')
}

tasks.named('noJREDistTar') {
    dependsOn tasks.named('downloadLicenses')
    dependsOn tasks.named('scriptsForRelease')
}

tasks.named('noJREDistZip') {
    dependsOn tasks.named('downloadLicenses')
    dependsOn tasks.named('scriptsForRelease')
}

tasks.named('linuxDistTar') {
    dependsOn tasks.named('downloadLicenses')
    dependsOn tasks.named('scriptsForRelease')
}

tasks.named('linuxDistZip') {
    dependsOn tasks.named('downloadLicenses')
    dependsOn tasks.named('scriptsForRelease')
    dependsOn tasks.named('extractOpenJFXForLibavPlugin')
    dependsOn tasks.named('extractLinuxJRE')
    dependsOn tasks.named('extractTobiiDriversForLinux')
    dependsOn tasks.named('unzipDistribution')
}

tasks.named('macosDistTar') {
    dependsOn tasks.named('downloadLicenses')
    dependsOn tasks.named('scriptsForRelease')
}

tasks.named('macosDistZip') {
    dependsOn tasks.named('downloadLicenses')
    dependsOn tasks.named('scriptsForRelease')
    dependsOn tasks.named('extractMacosJRE')
    dependsOn tasks.named('unzipDistribution')
}

tasks.named('windowsDistTar') {
    dependsOn tasks.named('downloadLicenses')
    dependsOn tasks.named('scriptsForRelease')
    dependsOn tasks.named('extractWindowsJRE')
    dependsOn tasks.named('unzipDistribution')
}

tasks.named('windowsDistZip') {
    dependsOn tasks.named('downloadLicenses')
    dependsOn tasks.named('scriptsForRelease')
}


tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    excludeFilter = file("spotbugs-exclude.xml")
}

tasks.withType(Tar){
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}

distZip {
    duplicatesStrategy = 'exclude'
}
[bootJar, bootDistTar, bootDistZip]*.enabled = false

release {
    failOnPublishNeeded = false
    failOnCommitNeeded = false
}

mainClassName = 'application.Main'

defaultTasks 'clean', 'build'
startScripts.enabled = false

afterReleaseBuild.dependsOn generateWindowsInstallerInDocker
